#!ipxe

set node_model pc-x86-64
# This is provided by iPXE, extracted from BIOS information.
set node_submodel ${manufacturer}/${product}
echo Walt node model: ${node_model}
echo Walt node submodel: ${node_submodel}

# Compute url for kernel, initrd, and custom ipxe scripts
set kernel_url /nodes/${mac:hexhyp}/${node_model}/${node_submodel}/kernel
set kernel_fallback_url /nodes/${mac:hexhyp}/${node_model}/kernel
set initrd_url /nodes/${mac:hexhyp}/${node_model}/${node_submodel}/initrd
set initrd_fallback_url /nodes/${mac:hexhyp}/${node_model}/initrd
set ipxe_script /nodes/${mac:hexhyp}/${node_model}/boot-params.ipxe
set ipxe_script_submodel /nodes/${mac:hexhyp}/${node_model}/${node_submodel}/boot-params.ipxe

# Default value for variables (may be overriden by custom ipxe script)
set walt_init /bin/walt-init
# Interface name within the booted OS on which netconsole should send logs
set netconsole_iface eth0
# Custom kernel parameters directly passed to the kernel command line (useful for serial console)
set additional_kernel_params

# Load custom ipxe scripts: model, then submodel.
echo Loading custom ipxe scripts from Walt image
# imgfree is necessary, otherwise the loaded ipxe script corrupts the memory of the initrd.
chain /nodes/${mac:hexhyp}/${node_model}/boot-params.ipxe && imgfree boot-params.ipxe ||
chain /nodes/${mac:hexhyp}/${node_model}/${node_submodel}/boot-params.ipxe && imgfree boot-params.ipxe ||

# Compute kernel command line args
set nfs_root ${next-server}:/var/lib/walt/nodes/${mac}

# Specifying the NFS version is necessary for initrd-less images: the
# kernel defaults to NFS version 2 and somehow panics even before
# running init. Images with initrd work fine because they negotiate the
# version with the server, starting with NFSv4.
# We would normally use "nfsvers=3" here, but this is not understood
# by Debian's initrd (nfsmount from klibc).  So, use the old "v3"
# syntax, which is understood both by the kernel (for initrd-less
# images) and by Debian's initrd.
set nfs_bootargs root=/dev/nfs nfsroot=${nfs_root},v3

set walt_bootargs walt.node.model=${node_model} walt.server.ip=${next-server}

# For now, Walt's logging module (NetconsoleListener) does not know
# how to decode the extended format from netconsole. We should enable
# the extended format ("netconsole=+1234...") when this is the case,
# because it will allow proper handling of continuation lines.
# References:
# https://www.kernel.org/doc/Documentation/networking/netconsole.txt
# https://www.kernel.org/doc/Documentation/ABI/testing/dev-kmsg
set console_bootargs netconsole=1234@${ip}/${netconsole_iface},12342@${next-server}/

# BOOTIF is set by pxelinux to the MAC address of the interface used to boot,
# and is understood by Debian's initrd scripts.  Use the same syntax as pxelinux
# to be compatible.
set other_bootargs init=${walt_init} panic=30 ip=dhcp BOOTIF=01-${mac:hexhyp}

set bootargs ${nfs_bootargs} ${walt_bootargs} ${console_bootargs} ${other_bootargs} ${additional_kernel_params}

echo Loading optional initrd...
initrd ${initrd_url} || initrd ${initrd_fallback_url} || echo No initrd found

# boot
echo Booting kernel with parameters: ${bootargs}
boot ${kernel_url} ${bootargs} || boot ${kernel_fallback_url} ${bootargs} || reboot
